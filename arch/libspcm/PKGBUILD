# Maintainer: Yichao Yu <yyc1992@gmail.com>
pkgname=libspcm
pkgver=525.17926
_pkgver=v525b17926
pkgrel=2
pkgdesc="Userspace driver libraies for the Spectrum Instrumentation AWG"
arch=('x86_64')
url="https://spectrum-instrumentation.com"
license=('custom')
source=(https://spectrum-instrumentation.com/sites/default/files/download/spcm_linux_libs_${_pkgver}.tgz
        https://spectrum-instrumentation.com/sites/default/files/download/spcm_examples.tgz)
sha256sums=('4cce37a634e6fc39eccaaf6595fd8825739bf26b7a6fcd12083f1c7f55a0f278'
            'SKIP')

package() {
  install -dm755 "$pkgdir"/usr/{lib,include/spcm}
  install -Dm755 spcm_linux/libs/spcm_linux_64bit_stdc++6.so \
          "$pkgdir"/usr/lib/libspcm_linux.so

  cp -v spcm_examples/c_cpp/c_header/*.h "$pkgdir"/usr/include/spcm/
  rm "$pkgdir"/usr/include/spcm/{errors.h,spectrum.h}
  cat > "$pkgdir"/usr/include/spcm/spcm.h <<EOF
// Header to include all needed headers since the driver library header needs to be
// included in the right order....
// Unclear why they are not more user friendly.
#include "dlltyp.h"
#include "regs.h"
#include "spcerr.h"
#include "spcm_drv.h"
EOF
}
