pkgname=libnacs-git
pkgver=1192.d3fd75b
pkgrel=1
pkgdesc='Library used for the NaCs control system'
arch=(x86_64 armv7h aarch64)
url='http://github.com/nacs-lab/libnacs'
license=('LGPL')
options=('!strip' 'debug')
depends=(libopenlibm.so libyaml-cpp.so libzmq.so)
provides=(libnacs-utils.so libnacs-seq.so)
makedepends=(cmake git cppzmq)
source=("git+https://github.com/nacs-lab/libnacs")
md5sums=('SKIP')

if [[ $CARCH == armv7h ]]; then
  cmake_args=(-DENABLE_KNACS=On
              -DENABLE_SIMD=Off
              -DENABLE_LLVM=Off)
  depends+=(knacs)
  provides+=(libnacs-kernel.so)
else
  cmake_args=(-DENABLE_KNACS=Off
              -DENABLE_SIMD=On
              -DENABLE_LLVM=On)
  depends+=(intel-tbb llvm-libs sleef)
  makedepends+=(llvm)
fi


pkgver() {
  cd libnacs

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libnacs

  CFLAGS+=' -O3 -flto'
  CXXFLAGS+=' -O3 -flto -U_GLIBCXX_ASSERTIONS'
  LDFLAGS+=' -O3 -flto'

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"

  mkdir -p build
  cd build
  cmake .. "${cmake_args[@]}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_BINDIR=/usr/bin -DCMAKE_INSTALL_DATADIR=/usr/share \
        -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
        -DENABLE_TESTING=Off
  make
}

package() {
  optdepends=(openlibm)
  cd libnacs/build

  make install DESTDIR="${pkgdir}"

  ver=$(cat "${pkgdir}/usr/lib/pkgconfig/nacs-utils.pc" | grep '^Version: ')
  ver=${ver#Version: }
  # This extra assignment needed to fool the stupid context-independent validation
  # from `makepkg`.
  pv=("libnacs=$ver")

  provides=("${pv[@]}")
}
