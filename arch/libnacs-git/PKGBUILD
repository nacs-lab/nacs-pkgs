pkgname=libnacs-git
pkgver=1067.94386de
pkgrel=1
pkgdesc='Library used for the NaCs control system'
arch=(x86_64 armv7h aarch64)
url='http://github.com/nacs-lab/libnacs'
license=('LGPL')
options=('!strip' 'debug')
depends=(intel-tbb llvm-libs openlibm sleef yaml-cpp zeromq)
makedepends=(cmake git llvm)
source=("git://github.com/nacs-lab/libnacs#branch=seq")
md5sums=('SKIP')

pkgver() {
  cd libnacs

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libnacs

  CFLAGS+=' -O3 -flto'
  CXXFLAGS+=' -O3 -flto'
  LDFLAGS+=' -O3 -flto'

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"

  mkdir -p build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_BINDIR=/usr/bin -DCMAKE_INSTALL_DATADIR=/usr/share \
        -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
        -DENABLE_KNACS=Off \
        -DENABLE_LLVM=On \
        -DENABLE_TESTING=Off
  make
}

# check() {
#   cd libnacs/build

#   tools/nacs-cpudump
#   ctest -E /excluded --output-on-failure -j $(nproc)
# }

package() {
  optdepends=(openlibm)
  cd libnacs/build

  make install DESTDIR="${pkgdir}"

  ver=$(cat "${pkgdir}/usr/lib/pkgconfig/nacs-utils.pc" | grep '^Version: ')
  ver=${ver#Version: }
  # This extra assignment needed to fool the stupid context-independent validation
  # from `makepkg`.
  pv=("libnacs=$ver")

  provides=("${pv[@]}")
}
