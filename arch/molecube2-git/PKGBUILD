pkgname=molecube2-git
pkgver=295.36db3e2
pkgrel=2
pkgdesc='NaCs control'
arch=(x86_64 armv7h aarch64)
url='http://github.com/nacs-lab/molecube2'
license=('LGPL')
options=('!strip' 'debug')
makedepends=(cmake git cppzmq)
depends=(libnacs-utils.so libnacs-seq.so libyaml-cpp.so libzmq.so)
source=("git+https://github.com/nacs-lab/molecube2")
md5sums=('SKIP')

pkgver() {
  cd molecube2

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd molecube2

  CFLAGS+=' -O3 -flto'
  CXXFLAGS+=' -O3 -flto'
  LDFLAGS+=' -O3 -flto'

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd molecube2/build

  make install DESTDIR="${pkgdir}"

  # mkdir -p "${pkgdir}/usr/src/debug/molecube2/"{lib,server}

  # cp ../lib/*.{h,cpp} "${pkgdir}/usr/src/debug/molecube2/lib"
  # cp ../server/*.cpp "${pkgdir}/usr/src/debug/molecube2/server"
}
