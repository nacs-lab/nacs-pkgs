# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

pkgbase=mingw-w64-sleef-git
pkgname="${MINGW_PACKAGE_PREFIX}-sleef-git"
pkgrel=1
pkgver=3.3.1.47.g5d84bbf
pkgdesc="SIMD Library for Evaluating Elementary Functions, vectorized libm and DFT (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-sleef")
arch=('any')
url="http://sleef.org/"
license=('custom')
provides=("${MINGW_PACKAGE_PREFIX}-sleef")
conflicts=("${MINGW_PACKAGE_PREFIX}-sleef")
replaces=("${MINGW_PACKAGE_PREFIX}-sleef")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             patch make)
source=("git://github.com/shibatch/sleef"
        0001-Use-vector-calling-convention.patch
        0002-Use-cat-instead-of-type.patch)
sha256sums=('SKIP'
            '8d371d2599583e548ac239a66781ed214ca24b6275c65d660574d6ab58dd9764'
            'd3fca0181e9aa3663b11c6eb738badb79f2849ecde12a517486c902f5edef30b')

pkgver() {
  cd sleef

  git describe --tags | sed -e 's/-/./g'
}

prepare() {
  cd sleef

  git reset --hard HEAD
  git clean -fdx

  patch -Np1 < ../0001-Use-vector-calling-convention.patch
  patch -Np1 < ../0002-Use-cat-instead-of-type.patch
}

build() {
  cd sleef

  mkdir -p build
  cd build

  CC=${MINGW_PREFIX}/bin/clang CXX=${MINGW_PREFIX}/bin/clang++ \
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" "${MINGW_PREFIX}"/bin/cmake .. \
    -G"MSYS Makefiles" -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
  make
}

package() {
  cd sleef/build

  make install DESTDIR="$pkgdir"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mv "${pkgdir}${MINGW_PREFIX}/lib/"libsleef.dll "${pkgdir}${MINGW_PREFIX}/bin/"
}
