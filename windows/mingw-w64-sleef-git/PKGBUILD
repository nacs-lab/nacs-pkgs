# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

pkgbase=mingw-w64-sleef-git
pkgname="${MINGW_PACKAGE_PREFIX}-sleef-git"
pkgrel=1
pkgver=3.3.1.47.g5d84bbf
pkgdesc="SIMD Library for Evaluating Elementary Functions, vectorized libm and DFT (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-sleef")
arch=('any')
url="http://sleef.org/"
license=('custom')
provides=("${MINGW_PACKAGE_PREFIX}-sleef")
conflicts=("${MINGW_PACKAGE_PREFIX}-sleef")
replaces=("${MINGW_PACKAGE_PREFIX}-sleef")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             patch make)
source=("git://github.com/shibatch/sleef"
        0001-Use-addSuffix-in-place-of-cat-type.patch
        0002-Use-vector-calling-convention.patch)
sha256sums=('SKIP'
            '8992b4b0c01d8639c84c05ebb759cacb131298d3756df933b76bc6011a556249'
            '66813be9393f05c60ce2d87c5bcc8156ace4cfa753ed99abac3655a61af6a18d')

pkgver() {
  cd sleef

  git describe --tags | sed -e 's/-/./g'
}

prepare() {
  cd sleef

  git reset --hard HEAD
  git clean -fdx

  patch -Np1 < ../0001-Use-addSuffix-in-place-of-cat-type.patch
  patch -Np1 < ../0002-Use-vector-calling-convention.patch
}

build() {
  cd sleef

  mkdir -p build
  cd build

  CC=${MINGW_PREFIX}/bin/clang CXX=${MINGW_PREFIX}/bin/clang++ \
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" "${MINGW_PREFIX}"/bin/cmake .. \
    -G"MSYS Makefiles" -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
  make
}

package() {
  cd sleef/build

  make install DESTDIR="$pkgdir"
}
