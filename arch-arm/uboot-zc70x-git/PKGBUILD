# Maintainer: Ettore Chimenti <ek5.chimenti at gmail dot com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
pkgname=uboot-zc70x-git
pkgver=2015.1.0.1828.g1160fbc
pkgrel=1
pkgdesc='Utilities for working with Das U-Boot Compiled for zc70x'
arch=('armv7h')
url="http://www.denx.de/wiki/U-Boot/WebHome"
license=('GPL2')
makedepends=('arm-linux-gnueabihf-gcc')
provides=('uboot-zc70x')
conflicts=('uboot-zc70x')
source=("git://github.com/Xilinx/u-boot-xlnx"
  0001-python-use-python2.patch)
md5sums=('SKIP'
         '9ce7d13cdffe3780ac108a46443625d0')
options=('strip' 'debug')

prepare() {
  cd u-boot-xlnx

  patch -Np1 < ../0001-python-use-python2.patch

  rm -rf "${srcdir}/bin"
  mkdir -p "${srcdir}/bin"
  for c in cc gcc; do
    cat > "${srcdir}/bin/${c}" <<EOF
#!/bin/bash
exec arm-linux-gnueabihf-gcc $CFLAGS --sysroot=$SYSROOT -Wl,-rpath-link,/usr/arm-linux-gnueabihf/lib:$SYSROOT/usr/lib "\$@"
EOF
    chmod +x "${srcdir}/bin/${c}"
  done
  for c in c++ g++; do
    cat > "${srcdir}/bin/${c}" <<EOF
#!/bin/bash
exec arm-linux-gnueabihf-g++ $CXXFLAGS --sysroot=$SYSROOT -Wl,-rpath-link,/usr/arm-linux-gnueabihf/lib:$SYSROOT/usr/lib "\$@"
EOF
    chmod +x "${srcdir}/bin/${c}"
  done
}

pkgver() {
  cd u-boot-xlnx

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build(){
  cd u-boot-xlnx
  export PATH="${srcdir}/bin:${PATH}"

  make zynq_zc70x_config ARCH=arm CROSS_COMPILE=arm-linux-gnueabihf-

  make ARCH=arm CROSS_COMPILE=arm-linux-gnueabihf-
}

package() {
  cd u-boot-xlnx
  export PATH="${srcdir}/bin:${PATH}"

  install -Dm755 tools/mkimage "${pkgdir}/usr/bin/mkimage"
  install -Dm755 u-boot "${pkgdir}/boot/u-boot"
  install -Dm644 doc/mkimage.1 \
    "${pkgdir}/usr/share/man/man1/mkimage.1"
}
