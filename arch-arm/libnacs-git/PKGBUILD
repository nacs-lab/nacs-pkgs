pkgname=libnacs-git
pkgver=1074.7e56ba2
pkgrel=1
pkgdesc='Library used for the NaCs control system'
arch=('armv7h' 'aarch64')
url='http://github.com/nacs-lab/libnacs'
license=('LGPL')
options=('!strip' 'debug')
makedepends=('cmake' 'git' "${CHOST}-gcc" 'arch-arm')
provides=(libnacs-utils.so libnacs-seq.so)
conflicts=(libnacs)
source=("git+https://github.com/nacs-lab/libnacs")
md5sums=('SKIP')

pkgver() {
  cd libnacs

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libnacs

  CFLAGS=${CFLAGS/-fvar-tracking-assignments/}
  CXXFLAGS=${CXXFLAGS/-fvar-tracking-assignments/}
  if [[ $CARCH == armv7h ]]; then
    CFLAGS=${CFLAGS/-march=armv7-a/}
    CXXFLAGS=${CXXFLAGS/-march=armv7-a/}
    cmake_args=(-DCMAKE_TOOLCHAIN_FILE=/usr/share/cmake/Modules/ARMv7hf.cmake
                -DENABLE_KNACS=On
                -DENABLE_SIMD=Off
                -DENABLE_LLVM=Off)
  else
    cmake_args=(-DCMAKE_TOOLCHAIN_FILE=/usr/share/cmake/Modules/ARMv8.cmake
                -DENABLE_KNACS=Off
                -DENABLE_SIMD=On
                -DENABLE_LLVM=On)
  fi

  CFLAGS+=' -O3 -flto'
  CXXFLAGS+=' -O3 -flto'
  LDFLAGS+=' -O3 -flto'

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"

  mkdir -p build-${CARCH}
  cd build-${CARCH}
  cmake .. "${cmake_args[@]}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_BINDIR=/usr/bin -DCMAKE_INSTALL_DATADIR=/usr/share \
        -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
        -DENABLE_TESTING=Off
  make
}

package() {
  depends=(libopenlibm.so libyaml-cpp.so libzmq.so)
  if [[ $CARCH == armv7h ]]; then
    depends+=(knacs)
    provides+=(libnacs-kernel.so)
  else
    depends+=(intel-tbb llvm-libs sleef)
  fi
  cd libnacs/build-${CARCH}

  make install DESTDIR="${pkgdir}"

  ver=$(cat "${pkgdir}/usr/lib/pkgconfig/nacs-utils.pc" | grep '^Version: ')
  ver=${ver#Version: }
  # This extra assignment needed to fool the stupid context-independent validation
  # from `makepkg`.
  pv=("libnacs=$ver")

  provides+=("${pv[@]}")
}
