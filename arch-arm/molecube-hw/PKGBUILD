pkgname=molecube-hw
pkgver=210.660cccf
pkgrel=2
pkgdesc='NaCs control'
arch=('armv7h')
url='harvard.edu'
license=('LGPL')
_vitis_ver=2020.1
makedepends=('cmake' "vitis=$_vitis_ver")
source=("git://github.com/nacs-lab/molecube-hw"
        "git://github.com/Xilinx/device-tree-xlnx#tag=xilinx-v$_vitis_ver")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd molecube-hw

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd molecube-hw

  rm -rf build/
  mkdir build
}

build() {
  cd molecube-hw

  (
    unset LANG
    unset QT_PLUGIN_PATH
    . /opt/Xilinx/Vivado/$_vitis_ver/settings64.sh
    . /opt/Xilinx/Vitis/$_vitis_ver/settings64.sh

    vivado -mode batch -nolog -source script/init.tcl -tclargs \
           -repo .
    vivado -mode batch -nolog -source script/build.tcl -tclargs \
           -proj project_molecube/project_molecube.xpr \
           -bit build/system.bit.bit \
           -hw build/system.xsa

    xsct script/gen_dts.tcl -hw build/system.xsa \
         -repo_dir ../device-tree-xlnx -out_dir build/dts/

    script/compile_dts.sh build/dts/system-top.dts build/dts/system.dtb
  )
}

package() {
  cd molecube-hw/build

  install -dm755 "${pkgdir}/boot/"
  install -T -Dm644 system.bit.bin "${pkgdir}/boot/system.bit.bin"
  install -T -Dm644 dts/system.dtb "${pkgdir}/boot/devicetree.dtb"
}
